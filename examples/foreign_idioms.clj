(ns examples.foreign-idioms
  (:require
   [zero-one.geni.core :as g]
   [zero-one.geni.test-resources :refer [melbourne-df]]))

(def dataframe melbourne-df)

;; Pandas
(g/shape dataframe)
;;=> [13580 21]

(-> dataframe
    (g/select :SellerG :Suburb)
    g/value-counts
    (g/limit 5)
    g/show)

;;=>
;; +-------+--------------+-----+
;; |SellerG|Suburb        |count|
;; +-------+--------------+-----+
;; |Biggin |Richmond      |101  |
;; |Nelson |Essendon      |97   |
;; |Nelson |Brunswick     |96   |
;; |Barry  |Reservoir     |90   |
;; |Buxton |Bentleigh East|88   |
;; +-------+--------------+-----+

(-> dataframe
    (g/select {:price-bins (g/cut :Price [8e5 1e6 1.2e6])})
    g/value-counts
    g/show)

;;=>
;; +---------------------------+-----+
;; |price-bins                 |count|
;; +---------------------------+-----+
;; |Price[-Infinity, 800000.0] |5479 |
;; |Price[1200000.0, Infinity] |4328 |
;; |Price[800000.0, 1000000.0] |2358 |
;; |Price[1000000.0, 1200000.0]|1415 |
;; +---------------------------+-----+

(-> dataframe
    (g/select {:price :Price
               :bins  (g/qcut :Price [0.1 0.5 0.9])})
    (g/group-by :bins)
    (g/agg {:min   (g/min :price)
            :mean  (g/mean :price)
            :max   (g/max :price)
            :count (g/count "*")})
    g/show)

;;=>
;; +---------------+---------+------------------+---------+-----+
;; |bins           |min      |mean              |max      |count|
;; +---------------+---------+------------------+---------+-----+
;; |Price[0.0, 0.1]|85000.0  |392448.1684981685 |480000.0 |1365 |
;; |Price[0.1, 0.5]|480500.0 |697254.5108695652 |903000.0 |5428 |
;; |Price[0.5, 0.9]|904000.0 |1270420.509825528 |1850000.0|5445 |
;; |Price[0.9, 1.0]|1851000.0|2511148.2026825636|9000000.0|1342 |
;; +---------------+---------+------------------+---------+-----+

(-> dataframe
    (g/select {:price :Price
               :bins  (g/qcut :Price 7)})
    (g/group-by :bins)
    (g/agg {:min   (g/min :price)
            :mean  (g/mean :price)
            :max   (g/max :price)
            :count (g/count "*")})
    g/show)

;;=>
;; +----------------------------------------------+---------+------------------+---------+-----+
;; |bins                                          |min      |mean              |max      |count|
;; +----------------------------------------------+---------+------------------+---------+-----+
;; |Price[0.0, 0.14285714285714285]               |85000.0  |426441.6469072165 |532000.0 |1940 |
;; |Price[0.14285714285714285, 0.2857142857142857]|532500.0 |611868.1180412371 |681500.0 |1940 |
;; |Price[0.2857142857142857, 0.42857142857142855]|682000.0 |754217.2118556701 |825000.0 |1940 |
;; |Price[0.42857142857142855, 0.5714285714285714]|826000.0 |910600.6593951413 |1000000.0|2017 |
;; |Price[0.5714285714285714, 0.7142857142857143] |1000001.0|1132074.017637627 |1265000.0|1871 |
;; |Price[0.7142857142857143, 0.8571428571428571] |1266000.0|1431798.5230296827|1650000.0|1954 |
;; |Price[0.8571428571428571, 1.0]                |1651000.0|2282461.4874869655|9000000.0|1918 |
;; +----------------------------------------------+---------+------------------+---------+-----+

;; NumPy
(-> dataframe
    (g/select {:land-size :LandSize
               :clipped   (g/clip :LandSize 100 200)})
    (g/limit 10)
    g/show)

;;=>
;; +---------+-------+
;; |land-size|clipped|
;; +---------+-------+
;; |202.0    |200.0  |
;; |156.0    |156.0  |
;; |134.0    |134.0  |
;; |94.0     |100.0  |
;; |120.0    |120.0  |
;; |181.0    |181.0  |
;; |245.0    |200.0  |
;; |256.0    |200.0  |
;; |0.0      |100.0  |
;; |220.0    |200.0  |
;; +---------+-------+

;; tech.ml.dataset
(-> [{:a 1 :b 2} {:a 2 :c 3}]
    g/->dataset
    g/show)

;;=>
;; +---+----+----+
;; |a  |b   |c   |
;; +---+----+----+
;; |1  |2   |null|
;; |2  |null|3   |
;; +---+----+----+

(-> "test/resources/melbourne_housing_snapshot.parquet"
    (g/->dataset {:n-records 5 :column-whitelist [:Price
                                                  :Suburb
                                                  :LandSize]})
    g/show)

;;=>
;; +---------+----------+--------+
;; |Price    |Suburb    |LandSize|
;; +---------+----------+--------+
;; |1480000.0|Abbotsford|202.0   |
;; |1035000.0|Abbotsford|156.0   |
;; |1465000.0|Abbotsford|134.0   |
;; |850000.0 |Abbotsford|94.0    |
;; |1600000.0|Abbotsford|120.0   |
;; +---------+----------+--------+
